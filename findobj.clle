PGM PARM(&obj &lib &type &rtn)
/* CL procedure for finding object. Returns output. */
/* Variables *****************************************************************/
DCL VAR(&obj) TYPE(*CHAR) LEN(10) /* Object name */
DCL VAR(&lib) TYPE(*CHAR) LEN(10) /* Library qualifer */
DCL VAR(&type) TYPE(*CHAR) LEN(10) /* Type of object ex. *FILE */
DCL VAR(&rtn) TYPE(*LGL) /* The return value for calling program */
DCL VAR(&subr0) TYPE(*INT) LEN(4) /* The return value for subroutines */
DCL VAR(&DOU) TYPE(*LGL) VALUE('1') /* We'll want to LEAVE DO...ENDDO, +
    so we have to use DOUNTIL...ENDDO instead. */
/*****************************************************************************/
/* Program Mainline **********************************************************/
DOUNTIL &DOU
    IF COND(&obj *EQ ' ') THEN(LEAVE)
    IF COND(&lib *EQ ' ') THEN(CHGVAR &lib '*LIBL')
    IF COND(&type *EQ ' ') THEN(CHGVAR &type '*FILE')
    CALLSUBR SUBR(FINDOBJ) RTNVAL(&subr0)
    IF COND(&subr0 *EQ 1) THEN(CHGVAR &rtn '1')
    ELSE CMD(CHGVAR &rtn '0')
ENDDO
/*****************************************************************************/
/* Subroutines ***************************************************************/
SUBR SUBR(FINDOBJ)
    CHKOBJ OBJ(&lib/&obj) OBJTYPE(&type)
    MONMSG MSGID(CPF9801 CPF9810) EXEC(RTNSUBR RTNVAL(0))
    RTNSUBR RTNVAL(1)
ENDSUBR
/*****************************************************************************/
ENDPGM